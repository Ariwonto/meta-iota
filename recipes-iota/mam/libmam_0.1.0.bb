SUMMARY = "IOTA Low Level implementation of Masked Authentication Messaging (MAM)"
DESCRIPTION = "MAM (Masked Authenticated Messaging) is a trinary messages encryption scheme that allows for both confidentiality and authenticity based on an original implementation & specification by apmi.bsu.by \
MAM allows for: \
 - Message encryption via NTRU (public key encrytpion) and PSK (Pre-Shared Key) \
 - Sending a message to multiple receivers, each having his/her own session key \
 - Authenticating a message either via MAC (Integrity) or via MSS signature (Integrity + Authenticity)\
"

LICENSE = "Apache-2.0"
LIC_FILES_CHKSUM = "file://${COMMON_LICENSE_DIR}/Apache-2.0;md5=89aea4e17d99a7cacdbeed46a0096b10"

SRC_URI = " \
            git://github.com/iotaledger/entangled.git;branch=develop \
	    file://0001-add-YOCTO-option-to-CMakeLists.txt.patch \
	    file://0002-flex-trit-encoding-3-trits-per-byte.patch \
	    file://CMakeLists.txt \   
	    file://trit_t_to_mam_msg.tar.gz \
	    file://hash_containers.tar.gz \
	    file://mam_containers.tar.gz \
"

SRCREV = "${AUTOREV}"

DEPENDS = "keccak uthash mbedtls logger http-parser cjson"

inherit cmake

EXTRA_OECMAKE = "-DYOCTO=ON -DCCLIENT_TEST=OFF "

S = "${WORKDIR}/git"

do_configure_prepend(){

    # set mam CMakeLists
    cp ${WORKDIR}/CMakeLists.txt ${S}/mam

    # set maps and containers originally generated by Bazel
    cp ${WORKDIR}/trit_t_to_mam_msg_* ${S}/mam/api
    cp ${WORKDIR}/mam_channel_t_set.* ${S}/mam/mam
    cp ${WORKDIR}/mam_endpoint_t_set.* ${S}/mam/mam
    cp ${WORKDIR}/mam_pk_t_set.* ${S}/mam/mam
    cp ${WORKDIR}/mam_ntru_pk_t_set.* ${S}/mam/ntru
    cp ${WORKDIR}/mam_ntru_sk_t_set.* ${S}/mam/ntru
    cp ${WORKDIR}/mam_psk_t_set.* ${S}/mam/psk

    # set hash containers generated by cmake/gen_hash_containers.sh
    cp ${WORKDIR}/hash*.c ${S}/utils/containers/hash
    cp ${WORKDIR}/hash*.h ${S}/utils/containers/hash
}

# leaving the headers on /usr/include/entangled causes incompatibility with the includes
do_install_append(){
    mv ${D}${includedir}/entangled/* ${D}${includedir}
}
